@echo off
setlocal
pushd "%~dp0"
REM ============================================================
REM  build_wazamono.bat   (cmd-native, ASCII-only auto-detect)
REM  WazamonoCore fork - build the USB CDC bootloader for the
REM  Wazamono product boards.
REM
REM  This does NOT modify the clean-room bootloader source. The only
REM  per-board build differences are passed in at build time:
REM
REM    board             MCU         LED   pol       USB ident (VID:PID)
REM    ----------------  ----------  ----  --------  -------------------
REM    Wazamono Tachi    avr64du32   PD5   act-LOW   0x1209:0x0005
REM    Wazamono Tsurugi  avr64du32   PD6   act-HIGH  0x1209:0x0007
REM    Wazamono Kunai    avr32du20   PD4   act-LOW   0x1209:0x0009
REM
REM    - LED pin     : LED_PORT / LED_PIN
REM    - LED polarity: LED_AH=1 (active-HIGH) | LED_AL=1 (active-LOW)
REM                    Neither given => active-LOW; both given => LED_AH wins.
REM    - USB identity: BOARD=TACHI | TSURUGI  (selects PID + product string)
REM
REM  Tachi's LED (PD5) is active-LOW (Pro Micro RX/TX convention); Tsurugi's
REM  LED (PD6 = D13, op-amp buffered) is active-HIGH (Arduino Uno convention).  Each board
REM  passes its polarity explicitly below.
REM
REM  The signature is read from SIGROW at runtime (stk500.c), so the single
REM  avr64du32 hex serves every Wazamono board that shares the 64 KB flash.
REM
REM  Put this in  WazamonoCore\megaavr\bootloaders\usbcdcboot\  and run.
REM ============================================================

REM --- find avr-gcc under C:\avr-gcc (version auto-detected) ------------
REM  The avr-gcc 15.2 toolchain lives at  C:\avr-gcc\avr-gcc-15.2.0\ .
REM  Override the root with:  set "AVRGCC_ROOT=D:\path"  before running.
if not defined AVRGCC_ROOT set "AVRGCC_ROOT=C:\avr-gcc"
set "GCCBIN="
for /d %%d in ("%AVRGCC_ROOT%\avr-gcc-*") do set "GCCBIN=%%~fd\bin"
if not defined GCCBIN if exist "%AVRGCC_ROOT%\bin\avr-gcc.exe" set "GCCBIN=%AVRGCC_ROOT%\bin"
if not defined GCCBIN (
  echo ERROR: no avr-gcc-*\bin found under "%AVRGCC_ROOT%"
  echo   Put the toolchain at  C:\avr-gcc\avr-gcc-15.2.0\   ^(or set AVRGCC_ROOT^).
  popd ^& exit /b 1
)
if not exist "%GCCBIN%\avr-gcc.exe" (
  echo ERROR: avr-gcc.exe missing in "%GCCBIN%"
  popd ^& exit /b 1
)
set "PATH=%GCCBIN%;%PATH%"
if not defined MAKE set MAKE=make

REM            class             mcu        LEDport LEDpin board     LEDpol(AH|AL) VREG(0|1)
call :build wazamonotachi     avr64du32  PORTD   5      TACHI   AL 0
call :build wazamonotsurugi   avr64du32  PORTD   6      TSURUGI AH 1
call :build wazamonokunai     avr32du20  PORTD   4      KUNAI   AL 0

echo.
echo === collecting hex files into ..\hex\ ===
if not exist "..\hex" mkdir "..\hex"
move /y usbcdcboot_*.hex "..\hex\" >nul

echo.
echo === hex files in ..\hex\ ===
dir /b "..\hex\usbcdcboot_wazamono*.hex"
popd
endlocal
goto :eof

:build
REM  %1=class tag  %2=mcu  %3=LED port  %4=LED pin  %5=board tag  %6=LED pol (AH | AL)  %7=VREG (0 | 1)
set "POLFLAG="
if /i "%~6"=="AH" set "POLFLAG=LED_AH=1"
if /i "%~6"=="AL" set "POLFLAG=LED_AL=1"
set "VREGVAL=%~7"
if not defined VREGVAL set "VREGVAL=0"
echo.
echo ------ building %2  -^> usbcdcboot_%1.hex  (LED %3 %4, board %5 %6, VREG %VREGVAL%) ------
del /q src\*.o 2>nul
del /q usbcdcboot_%1.elf usbcdcboot_%1.hex usbcdcboot_%1.lst usbcdcboot_%1.map 2>nul
"%MAKE%" MCU=%2 TARGET=usbcdcboot_%1 LED_PORT=%3 LED_PIN=%4 BOARD=%5 VREG=%VREGVAL% %POLFLAG% all
"%MAKE%" MCU=%2 TARGET=usbcdcboot_%1 BOARD=%5 VREG=%VREGVAL% %POLFLAG% size
goto :eof
